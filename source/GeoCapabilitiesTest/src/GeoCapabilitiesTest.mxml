<?xml version="1.0" encoding="utf-8"?>
<!-- Copyright (C) 2014 RealEyes Media LLC. -->
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   applicationComplete="_onApplicationComplete(event)">
	
	<fx:Script>
		<![CDATA[
			import com.turbidwater.geocapabilities.GeoCapabilities;
			import com.turbidwater.geocapabilities.events.ProviderStatusChangeEvent;
			import com.turbidwater.geocapabilities.events.ProximityAlertEvent;
			import com.turbidwater.geocapabilities.model.GPSStatus;
			import com.turbidwater.geocapabilities.model.NetworkStatus;
			
			import flash.sensors.Geolocation;
			
			import mx.events.FlexEvent;
			
			public var geo:GeoCapabilities;
			public var airGeo:Geolocation;
			
			protected var monitoringNetwork:Boolean;
			
			public function checkStatus():void
			{
				gpsOn_lbl.text = "Is GPS On? " + GeoCapabilities.isGPSEnabled;
				networkOn_lbl.text = "Is Network Location On? " + GeoCapabilities.isNetworkEnabled;
			}
			
			public function startGPSMonitoring():void
			{
				if( Geolocation.isSupported && !airGeo )
				{
					airGeo = new Geolocation();
					airGeo.addEventListener( GeolocationEvent.UPDATE, _onGPSUpdate );
					airGeo.setRequestedUpdateInterval( 20000 );
				}
			}
			
			public function stopGPSMonitoring():void
			{
				if( airGeo )
				{
					airGeo.removeEventListener( GeolocationEvent.UPDATE, _onGPSUpdate );
					airGeo = null;
				}
			}
			
			private function _onApplicationComplete( event:FlexEvent ):void
			{
				geo = new GeoCapabilities();
				geo.addEventListener( StatusEvent.STATUS, _onStatusEvent );
				geo.addEventListener( ProviderStatusChangeEvent.GPS_STATUS_CHANGED, _onGPSStatusEvent );
				geo.addEventListener( ProviderStatusChangeEvent.NETWORK_STATUS_CHANGED, _onNetworkStatusEvent );
				geo.addEventListener( ProximityAlertEvent.PROXIMITY_ALERT, _onProximityAlert );
				
				checkStatus();
			}
			
			private function _onStatusEvent( event:StatusEvent ):void
			{
				trace( 'test received status event ' + event.level );
			}
			
			private function _onGPSStatusEvent( event:ProviderStatusChangeEvent ):void
			{
				trace( 'GPS Status ' + event.status );
				if( event.status == GPSStatus.GPS_STARTED || event.status == GPSStatus.GPS_STOPPED )
				{
					checkStatus();
					if( event.status == GPSStatus.GPS_STOPPED )
					{
						gotFix_lbl.visible = false;
					}
				}
				else if( event.status == GPSStatus.GPS_FIRST_FIX )
				{
					gotFix_lbl.visible = true;
				}
				
			}
			
			private function _onNetworkStatusEvent( event:ProviderStatusChangeEvent ):void
			{
				trace( 'Network Status ' + event.status );
				if( event.status == NetworkStatus.AVAILABLE || event.status == NetworkStatus.ENABLED || 
					event.status == NetworkStatus.OUT_OF_SERVICE || event.status == NetworkStatus.DISABLED )
				{
					checkStatus();
				}
			}
			
			private function _onProximityAlert( event:ProximityAlertEvent ):void
			{
				proximityAlert_lbl.text = "Arrived at " + event.lat + ' ' + event.long;
			}
			
			private function _onGPSUpdate( event:GeolocationEvent ):void
			{
				
			}
			
			private function _onGPSToggle( event:MouseEvent ):void
			{
				if( !airGeo )
				{
					startGPSMonitoring();
					gps_btn.label = "Stop GPS Monitoring";
				}
				else
				{
					stopGPSMonitoring();
					gps_btn.label = "Start GPS Monitoring";
				}
			}
			
			private function _onNetworkToggle( event:MouseEvent ):void
			{
				if( !monitoringNetwork )
				{
					geo.startNetworkProviderMonitoring();
					network_btn.label = "Stop Network Monitoring";
				}
				else
				{
					geo.stopNetworkProviderMonitoring();
					network_btn.label = "Start Network Monitoring";
				}
				
				monitoringNetwork = !monitoringNetwork;
			}
			
			private function _onGetLastTimeToFix( event:MouseEvent ):void
			{
				var lastTimeToFix:int = geo.timeToFirstFix;
				var seconds:String = lastTimeToFix.toString();
				if( lastTimeToFix > 0 )
				{
					seconds = Math.round( lastTimeToFix/1000 ).toString();
				}
				
				lastFix_btn.label = "Last Time: " + seconds + "s";
			}
			
			private function _onSetProximityAlert( event:MouseEvent ):void
			{
				geo.addProximityAlert( Number( lat_ti.text ), Number( long_ti.text ), Number( radius_ti.text ), int( expiration_ti.text ) * 1000 );
			}
			
		]]>
	</fx:Script>
		
	<s:VGroup paddingBottom="15" paddingLeft="15" paddingRight="15" paddingTop="15">
		<s:Label id="gpsOn_lbl" text="Is GPS On?" />
		<s:Label id="networkOn_lbl" text="Is Network Location On?" />
		<s:Button id="check_btn" label="Check Status" click="checkStatus()" />
		<s:Button id="gps_btn" label="Start GPS Monitoring" click="_onGPSToggle( event )" />
		<s:Button id="network_btn" label="Start Network Monitoring" click="_onNetworkToggle( event )" />
		<s:HGroup verticalAlign="middle">
			<s:Button id="lastFix_btn" label="Get Last Time to Fix" click="_onGetLastTimeToFix( event )" />
			<s:Label id="gotFix_lbl" text="Got GPS fix!" visible="false" />
		</s:HGroup>
		<s:HGroup verticalAlign="middle">
			<s:Label text="Lat: "/>
			<s:TextInput id="lat_ti" width="320" text="39.739518" />
		</s:HGroup>
		<s:HGroup verticalAlign="middle">
			<s:Label text="Long: "/>
			<s:TextInput id="long_ti" width="320" text="-104.985612" />
		</s:HGroup>
		<s:HGroup verticalAlign="middle">
			<s:Label text="Radius (Meters): "/>
			<s:TextInput id="radius_ti" width="320" text="15" />
		</s:HGroup>
		<s:HGroup verticalAlign="middle">
			<s:Label text="Expiration (Seconds): "/>
			<s:TextInput id="expiration_ti" width="320" text="300" />
		</s:HGroup>
		<s:Button id="setProximityAlert_btn" label="Set Proximity Alert" click="_onSetProximityAlert( event )" />
		<s:Label id="proximityAlert_lbl" text="" />
	</s:VGroup>
	
</s:Application>